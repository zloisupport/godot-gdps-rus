# Скриптинг игры

В этом разделе вы создадите скрипты, необходимые для совместной работы всех элементов игры. 

Поток игры будет следующим:

1. Разместить мяч на Tee.
2. Переключиться в режим прицеливания и анимировать стрелку, пока игрок не щелкнет.
3. Переключиться в режим силы и анимировать шкалу силы, пока игрок не щелкнет.
4. Запустить мяч.
5. Повторять процесс с шага 2, пока мяч не упадет в лунку.

## Код UI 

Добавьте этот скрипт к экземпляру интерфейса, чтобы обновить элементы интерфейса.


```gdscript

extends CanvasLayer
@onready var power_bar = $MarginContainer/VBoxContainer/PowerBar
@onready var shots = $MarginContainer/VBoxContainer/Shots
var bar_textures = {
    "green": preload("res://assets/bar_green.png"),
    "yellow": preload("res://assets/bar_yellow.png"),
    "red": preload("res://assets/bar_red.png")
}
func update_shots(value):
    shots.text= "Shots: %s" % value

func update_power_bar(value):
    power_bar.texture_progress = bar_textures["green"] 
    if value > 70:
        power_bar.texture_progress = bar_textures["red"]
    elif value > 40:
        power_bar.texture_progress = bar_textures["yellow"]
    power_bar.value = value
    
func show_message(text):
    $Message.text = text
    $Message.show()
    await get_tree().create_timer(2).timeout
    $Message.hide()   
```
Эти функции позволяют обновлять элементы интерфейса, когда им нужно отобразить новое значение. Как и в Space Rocks, изменение текстуры прогресс-бара в зависимости от его значения дает приятное ощущение низкого/среднего/высокого уровня мощности.

## Cкрипт Main

Добавьте скрипт к сцене Hole и начните с этих переменных:

```gdscript
extends Node3D
enum {AIM, SET_POWER, SHOOT, WIN}
@export var power_speed = 100
@export var angle_speed = 1.1
var angle_change = 1
var power = 0
var power_change = 1
var shots = 0
var state = AIM
```

**enum** перечисляет состояния, в которых может находиться игра, а переменные power и angle будут использоваться для установки соответствующих значений и их изменения с течением времени. Вы можете управлять скоростью анимации (и, следовательно, сложностью), настраивая две экспортируемые переменные.

Далее, установите начальные значения перед началом игры:

```gdscript
func _ready():
    $Arrow.hide()
    $Ball.position = $Tee.position
    change_state(AIM)
    $UI.show_message("Get Ready!")
```


Мяч перемещается в положение клюшки, и вы переходите в состояние AIM для начала игры.

Вот что нужно сделать для каждого состояния игры:

```gdscript
func change_state(new_state):
    state = new_state
    match state:
    AIM:
        $Arrow.position = $Ball.position
        $Arrow.show()
    SET_POWER:
        power = 0
    SHOOT:
        $Arrow.hide()
        $Ball.shoot($Arrow.rotation.y, power / 15).
        shots += 1
        $UI.update_shots(shots)
    WIN:
        $Ball.hide()
        $Arrow.hide()
        $UI.show_message("Win!")
```

**AIM** размещает прицельную стрелку в положении мяча и делает ее видимой. Вспомните, что вы сместили стрелку, так что она будет выглядеть как будто указывает из мяча.Когда вы вращаете стрелку, вы будете вращать ее вокруг оси y, чтобы она оставалась параллельной земле.

При входе в состояние `SHOOT` вы вызываете функцию `shoot()` на мяче, которую вы еще не определили. Вы добавите это в следующем разделе.

Следующий шаг - проверить пользовательский ввод:

```gdscript
func _input(event):
    if event.is_action_pressed("click"): 
        match state:
            AIM:
                change_state(SET_POWER)
            SET_POWER:
                change_state(SHOOT)

```
Единственный (пока) элемент управления игрой - щелчок левой кнопкой мыши. 

В зависимости от того, в каком состоянии вы сейчас находитесь, щелчок по ней приведет к переходу в следующее состояние.

В функции ` _process()` вы будете определять, что анимировать, исходя из состояния. 
На данный момент она просто вызывает функцию, которая анимирует соответствующее свойство:

```gdscript
func _process(delta):
    match state:
        AIM:
            animate_arrow(delta)
        SET_POWER:
            animate_power(delta)
        SHOOT:
            pass
```
Обе эти функции похожи. Они постепенно изменяют значение между двумя крайностями, меняя направление, когда достигается предел. 
**Обратите внимание:** стрелка анимируется в диапазоне 180° (+90° до -90°):

```gdscript
func animate_arrow(delta):
    $Arrow.rotation.y += angle_speed * angle_change * delta
    if $Arrow.rotation.y > PI / 2:
        angle_change = -1
    if $Arrow.rotation.y < -PI / 2:
        angle_change = 1

func animate_power(delta):
    power += power_speed * power_change * delta
    if power >= 100:
        power_change = -1
    if power <= 0:
        power_change = 1
        $UI.update_power_bar(power)
```
Чтобы определить, когда мяч падает в лунку, выберите узел **Area3D**, который вы расположили у лунки,  и подключите его сигнал `body_entered`:

```gdscript
func _on_hole_body_entered(body):
    if body.name == "Ball": 
        print("win!")
        change_state(WIN)
```

Наконец, игроку нужно будет снова запустить весь процесс после того, как мяч остановится.

## Скрипт мяча

В скрипте мяча нужны две функции. Во-первых, к мячу необходимо приложить импульс, чтобы он начал движение. 
Во-вторых, когда мяч перестает двигаться, он должен уведомить главную сцену, чтобы игрок мог сделать следующий удар.

Убедитесь, что вы добавили этот скрипт к сцене **Ball**, а не к экземпляру мяча в сцене **Hole**:

```gdscript
extends RigidBody3D
signal stopped
func shoot(angle, power):
    var force = Vector3.FORWARD.rotated(Vector3.UP, angle)
    apply_central_impulse(force * power)

func _integrate_forces(state):
    if state.linear_velocity.length() < 0.1:
        stopped.emit()
        state.linear_velocity = Vector3.ZERO
    if position.y < -20:
        get_tree().reload_current_scene()
```

Как вы видели в игре **Space Rocks**, вы можете использовать физическое состояние в `_integrate_forces()` для безопасной остановки мяча,
если его скорость стала очень низкой. Из-за проблем с плавающей точкой скорость может не замедляться до 0 сама по себе.
Его значение `linear_velocity` может быть равно примерно 0.00000001 в течение довольно долгого времени после того, как он, кажется, остановится.
Вместо того, чтобы ждать, вы можете просто остановить мяч, если его скорость упадет ниже 0.1.

Существует также вероятность того, что мяч перепрыгнет через стену и упадет с курса.
Если это произойдет, вы можете перезагрузить сцену, чтобы позволить игроку начать заново.

Вернитесь в сцену **Hole** и подключите сигнал stopped экземпляра **Ball**:

```gdscript
func _on_ball_stopped():
    if state == SHOOT:
        change_state(AIM)
```

## Тестирование

Запустите сцену. Вы должны увидеть вращающуюся стрелку в положении мяча. 
Когда вы нажимаете кнопку мыши, стрелка останавливается, и шкала мощности начинает двигаться вверх и вниз. 
Когда вы снова нажимаете, мяч запускается.

Если какой-либо из этих шагов не работает, не продолжайте. Вернитесь и попытайтесь найти то, что вы пропустили в предыдущем разделе.

Как только все заработает, вы заметите некоторые области, которые нуждаются в улучшении. 
Во-первых, когда мяч перестает двигаться, стрелка может указывать не в том направлении, которое вам нужно. 
Причина в том, что начальный угол всегда равен 0, что указывает вдоль оси z, а затем стрелка качается +/-90° оттуда. 
В следующих двух разделах у вас будет выбор из двух вариантов улучшения прицеливания.

## Вариант 1: улучшение прицеливания

Прицеливание можно улучшить, направив 180-градусную дугу прямо в лунку в начале. 
Добавьте переменную `hole_dir` в начало скрипта. Вы можете найти это направление, используя немного векторной математики:

```gdscript
func set_start_angle():
    var hole_position = Vector2($Hole.position.z, $Hole.position.x)
    var ball_position = Vector2($Ball.position.z, $Ball.position.x)
    hole_dir = (ball_position - hole_position).angle() $Arrow.rotation.y = hole_dir
```


Помните, что положение мяча - это его центр, поэтому он находится чуть выше поверхности, в то время как центр лунки находится немного ниже. Из-за этого вектор, указывающий от мяча к лунке, также указывал бы под углом вниз в землю. Чтобы этого избежать и держать стрелку ровной, вы можете использовать только значения x и z из позиции для создания **Vector2**.

Теперь начальный угол можно установить при переходе в состояние AIM:

```gdscript
func change_state(new_state):
    state = new_state
    match state:
        AIM:
            $Arrow.position = $Ball.position
            $Arrow.show()
            set_start_angle()
```

Анимация стрелки может использовать это начальное направление в качестве основы для +/-90° колебания.
```gdscript
func animate_arrow(delta):
    $Arrow.rotation.y += angle_speed * angle_change * delta
    if $Arrow.rotation.y > hole_dir + PI / 2:
        angle_change = -1
    if $Arrow.rotation.y < hole_dir - PI / 2:
        angle_change = 1
```

## Вариант 2: улучшение прицеливания

Если вы предпочитаете иметь больше контроля над прицеливанием, 
то вместо того, чтобы анимировать стрелку и щелкать, чтобы установить прицел, 
вы можете напрямую управлять стрелкой, перемещая мышь из стороны в сторону.

Для достижения этого вы можете использовать один из типов **InputEvent** Godot: **InputEventMouseMotion**. 
Это событие происходит всякий раз, когда мышь перемещается, и оно включает в себя относительное свойство, 
означающее, насколько мышь переместилась в предыдущем кадре. Вы можете использовать это значение, чтобы немного повернуть стрелку.

Сначала отключите анимацию стрелки, удалив раздел AIM из `_process()`.

Для управления вращением стрелки на основе движения мыши добавьте переменную:
```gdscript
@export var mouse_sensitivity = 150
```

Затем добавьте следующий код в функцию  `_input()` для проверки движения мыши и вращения стрелки:
```gdscript
func _input(event):
    if event is InputEventMouseMotion:
        if state == AIM:
            $Arrow.rotation.y -= event.relative.x / mouse_sensitivity
```

## Захват мыши

Возможно, вы заметили, что при перемещении мыши она может выйти из окна игры, 
и при щелчке вы больше не взаимодействуете с игрой. Большинство 3D-игр решают эту проблему 
захватом мыши - фиксацией мыши в окне. Когда вы это делаете, вам также нужно дать игроку 
способ освободить мышь, чтобы он мог закрыть программу или щелкнуть другие окна, 
и способ повторного захвата, чтобы вернуться в игру.

Для этой игры вы сначала захватите мышь, а затем, если игрок нажмет Esc, освободите ее и 
приостановите игру. Нажатие в окне игры приведет к ее возобновлению.

Вся эта функциональность управляется через свойство Input.mouse_mode. 
Затем mouse_mode можно установить на одно из следующих значений:

* MOUSE_MODE_VISIBLE: Это режим по умолчанию. Мышь видна и может свободно перемещаться 
  внутри и вне окна.
* MOUSE_MODE_HIDDEN: Курсор мыши скрыт.
* MOUSE_MODE_CAPTURED: Мышь скрыта, а ее положение зафиксировано в окне.
* MOUSE_MODE_CONFINED: Мышь видна, но ограничена окном.

Для захвата мыши в начале игры добавьте следующую строку кода в функцию `_ready()`:

```gdscript
Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
```
В функции `_process()`, которая вызывается каждый кадр для обновления игры,
проверьте, захвачена ли мышь (т.е. находится ли она в режиме MOUSE_MODE_VISIBLE),
прежде чем обновлять элементы игры:

```gdscript
func _process(delta):
    if Input.mouse_mode == Input.MOUSE_MODE_VISIBLE: 
        return
```

Чтобы освободить мышь, добавьте следующее условие в функцию `_input()`

```gdscript
if event.is_action_pressed("ui_cancel") and Input.mouse_mode == Input.MOUSE_MODE_CAPT
    Input.mouse_mode = Input.MOUSE_MODE_VISIBLE
```

Чтобы повторно захватить мышь при щелчке в окне, добавьте следующий код
прямо перед блоком match_state:
```gdscript
    if event.is_action_pressed("click"):
        if Input.mouse_mode == Input.MOUSE_MODE_VISIBLE:
            Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
            return
```
Запустите сцену, чтобы попробовать.

