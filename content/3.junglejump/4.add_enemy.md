# Добавление врагов
Есть много различных поведений, которые можно добавить для врага. В этой игре враг будет идти по платформе в прямой линии и изменять направление при столкновении с препятствием.

## Настройка сцены
Как и раньше, вам нужно создать новую сцену для представления врага:


::list
  -  Начните с узла CharacterBody2D с именем Enemy и добавьте три дочерних узла: Sprite2D, CollisionShape2D и AnimationPlayer.
  -  Сохраните сцену в папку с именем enemies. Если вы решите добавить больше типов врагов в игру, вы можете сохранить их все здесь.
  -  Установите слой столкновения тела врага на enemies, а Маску на environment, player и enemies. Как и в случае с игроком, это определяет с какими типами объектов враг будет сталкиваться.
  -  Также полезно группировать врагов вместе, поэтому перейдите во вкладку Node и добавьте тело в группу с именем enemies.
  -  Добавьте res://assets/sprites/opossum.png в текстуру и установите Animation/Hframes в 6.
  -  Добавьте прямоугольную форму столкновения, которая покрывает большую часть (но не всю) изображения, убедившись, что нижняя часть формы столкновения выровнена с нижней частью ног опоссума:![опоссум](/img/jungle-jump/B19289_04_19.jpg)
  - Добавьте новую анимацию в AnimationPlayer с именем walk. Установите Длительность на 0,6 секунды и включите Петля и Автовоспроизведение при загрузке. 
  - У анимации walk должно быть две дорожки: одна, которая устанавливает свойство Texture узла Sprite2D, и другая, которая изменяет свойство Frame. Нажмите на значок ключа рядом с Texture, чтобы добавить первую дорожку, затем добавьте ключевые кадры для Frame 0 в момент времени 0 и Frame 5 в момент времени 0,5. Не забудьте изменить Режим обновления на Continue.
::

По завершении ваша анимация должна выглядеть так:

![Aнимация](/img/jungle-jump/B19289_04_20.jpg)


# Программируем врагов

На данный момент перемещение узла CharacterBody2D должно быть вам знакомо. Посмотрите на этот скрипт и попробуйте понять, что он делает, прежде чем прочитать предоставленное объяснение.
```gdscript
extends CharacterBody2D
@export var speed = 50
@export var gravity = 900
var facing = 1
func _physics_process(delta):
    velocity.y += gravity * delta
    velocity.x = facing * speed
    $Sprite2D.flip_h = velocity.x > 0
    move_and_slide()
    for i in get_slide_collision_count():
        var collision = get_slide_collision(i)
        if collision.get_collider().name == "Player":
            collision.get_collider().hurt()
        if collision.get_normal().x != 0:
            facing = sign(collision.get_normal().x)
            velocity.y = -100
    if position.y > 10000:
        queue_free()
```

В этом скрипте переменная facing отслеживает движение в направлении x, либо 1, либо -1. Как и с игроком, после перемещения необходимо проверить столкновения с поверхностью. Если сталкивается с объектом игрока, необходимо вызвать его функцию hurt().

Далее нужно проверить, имеет ли нормальный вектор столкнувшегося тела компонент x, отличный от 0. Это означает, что он направлен влево или вправо, что указывает на стену или другое препятствие. Направление нормали затем используется для установки нового значения facing. Придание телу небольшой вертикальной скорости создаст у врага небольшой эффект отскока при развороте, что выглядит более привлекательно.

Наконец, если по какой-то причине враг упадет с платформы, необходимо удалить врага, чья координата y станет слишком большой, чтобы избежать бесконечного отслеживания его падения.

Добавьте экземпляр Enemy в вашу сцену уровня. Убедитесь, что по обе стороны от него есть препятствия, и запустите сцену. Проверьте, что враг перемещается туда-сюда между препятствиями. Поставьте игрока на его путь и удостоверьтесь, что вызывается функция hurt() игрока.

Вы можете заметить, что если вы прыгнете на врага, ничего не произойдет. Этот аспект мы рассмотрим далее.

# Урон по врагов

Это нечестно, если у игрока нет возможности ответить ударом, поэтому, по традиции Марио, прыжок сверху на врага победит его.

Начните с добавления новой анимации в узел AnimationPlayer врага с именем death. Установите Длительность на 0,3 и Магнит на 0,05. Не включайте циклическое воспроизведение для этой анимации.

Анимация смерти также установит и Texture, и Frame. Перетащите изображение res://assets/sprites/enemy_death.png в область Texture спрайта, затем щелкните по ключу, чтобы добавить для него ключевой кадр. Как и раньше, установите ключевые кадры для значений 0 и 5 свойства Frame в начале и конце анимации. Не забудьте установить Режим обновления на Постоянный.

Добавьте следующий код в **enemy.gd**, чтобы у вас был способ запустить анимацию смерти на враге:

```gdscript
func take_damage():
    $AnimationPlayer.play("death")
    $CollisionShape2D.set_deferred("disabled", true)
    set_physics_process(false)
```


Когда игрок атакует врага при определенных условиях, вызывается функция take_damage(), которая воспроизводит анимацию смерти, отключает столкновения и останавливает движение.

Когда анимация смерти заканчивает воспроизведение, безопасно удалять врага, поэтому подключите сигнал animation_finished от AnimationPlayer:

![AAnimationPlayer](/img/jungle-jump/B19289_04_21.jpg)

Этот сигнал вызывается каждый раз, когда завершается любая анимация, поэтому необходимо проверить, что это именно та анимация:

```gdscript
func _on_animation_player_animation_finished(anim_name):
    if anim_name == "death":
        queue_free()
```

Чтобы завершить этот процесс, перейдите в скрипт player.gd и добавьте следующий код в раздел _physics_process(), где вы проверяете столкновения. Этот код проверит, если игрок ударил врага сверху:

```gdscript
for i in get_slide_collision_count():
    var collision = get_slide_collision(i)
    if collision.get_collider().is_in_group("danger"):
        hurt()
    if collision.get_collider().is_in_group("enemies"):
        if position.y < collision.get_collider().position.y:
            collision.get_collider().take_damage()
            velocity.y = -200
        else:
            hurt()
```

Этот код сравнивает положение y уровня ног игрока с положением y врага, чтобы определить, находится ли игрок сверху врага. Если да, то враг получит урон; в противном случае получит урон сам игрок.

Запустите уровень еще раз и попробуйте прыгнуть на врага, чтобы убедиться, что все работает как ожидается.

Скрипт игрока
Вы внесли несколько дополнений в скрипт игрока. Вот как теперь должен выглядеть полный скрипт:

```gdscript

extends CharacterBody2D
signal life_changed
signal died
@export var gravity = 750
@export var run_speed = 150
@export var jump_speed = -300
enum {IDLE, RUN, JUMP, HURT, DEAD}
var state = IDLE
var life = 3: set = set_life
func _ready():
    change_state(IDLE)
func change_state(new_state):
    state = new_state
    match state:
        IDLE:
            $AnimationPlayer.play("idle")
        RUN:
            $AnimationPlayer.play("run")
        HURT:
            $AnimationPlayer.play("hurt")
            velocity.y = -200
            velocity.x = -100 * sign(velocity.x)
            life -= 1
            await get_tree().create_timer(0.5).timeout
            change_state(IDLE)
        JUMP:
            $AnimationPlayer.play("jump_up")
        DEAD:
            died.emit()
            hide()
func get_input():
    if state == HURT:
        return
    var right = Input.is_action_pressed("right")
    var left = Input.is_action_pressed("left")
    var jump = Input.is_action_just_pressed("jump")
    # movement occurs in all states
    velocity.x = 0
    if right:
        velocity.x += run_speed
        $Sprite2D.flip_h = false
    if left:
        velocity.x -= run_speed
        $Sprite2D.flip_h = true
    # only allow jumping when on the ground
    if jump and is_on_floor():
        change_state(JUMP)
        velocity.y = jump_speed
    # IDLE transitions to RUN when moving
    if state == IDLE and velocity.x != 0:
        change_state(RUN)
    # RUN transitions to IDLE when standing still
    if state == RUN and velocity.x == 0:
        change_state(IDLE)
    # transition to JUMP when in the air
    if state in [IDLE, RUN] and !is_on_floor():
        change_state(JUMP)
func _physics_process(delta):
    velocity.y += gravity * delta
    get_input()
    move_and_slide()
    if state == HURT:
        return
    for i in get_slide_collision_count():
        var collision = get_slide_collision(i)
        if collision.get_collider().is_in_group("danger"):
            hurt()
        if collision.get_collider().is_in_group("enemies"):
            if position.y <
            collision.get_collider().position.y:
                collision.get_collider().take_damage()
                velocity.y = -200
            else:
                hurt()
    if state == JUMP and is_on_floor():
        change_state(IDLE)
    if state == JUMP and velocity.y > 0:
        $AnimationPlayer.play("jump_down")
func reset(_position):
    position = _position
    show()
    change_state(IDLE)
    life = 3
func set_life(value):
    life = value
    life_changed.emit(life)
    if life <= 0:
        change_state(DEAD)
func hurt():
    if state != HURT:
        change_state(HURT)

```

Если у вас возникли трудности с кодом игрока, попробуйте размышлить о том, какая часть может быть проблемой. Это движение? Обнаружение столкновений при встрече с врагом? Если вы можете узкое место проблемы, это поможет вам определить, на какой части скрипта вам следует сосредоточиться.

Убедитесь, что вы удовлетворены поведением игрока, прежде чем переходить к следующему разделу.