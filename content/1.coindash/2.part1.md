# Часть 1 – сцена игрока

Первая сцена, которую вы создадите, - это объект игрока. Одним из преимуществ создания отдельной сцены для игрока (и других объектов) является возможность тестирования его независимо, даже до создания других частей игры. Это разделение игровых объектов станет все более полезным по мере роста размеров и сложности ваших проектов. 

Хранение отдельных игровых объектов в отдельности облегчает их отладку, изменение и даже полное замещение без влияния на другие части игры. Это также означает, что ваш игрок может быть многократно используемым - вы можете вставить эту сцену игрока в совершенно другую игру, и она будет работать так же.

Сцена игрока должна выполнять следующие задачи:
::list

   - Отобразить вашего персонажа и его анимации

   - Откликаться на ввод пользователя, перемещая персонажа

   - Обнаруживать столкновения с другими игровыми объектами, такими как монеты или препятствия
::

## Cоздания сцены

Для создания сцены начните с нажатия кнопки **Add/Create a New Node** (сочетание клавиш **Ctrl + A)** и выбора узла **Area2D**. Затем щелкните по имени узла и измените его на **Player**. Нажмите **Scene -> Save Scene (Ctrl + S)**, чтобы сохранить сцену.

![](/img/1-coin/7.png)

Взгляните на вкладку FileSystem и обратите внимание, что теперь там появился файл **player.tscn**. Всякий раз, когда вы сохраняете сцену в Godot, она использует расширение **.tscn** – это формат файла для сцен Godot. "t" в названии означает "text" (текст), потому что это текстовые файлы. Если вас интересует, можете посмотреть его во внешнем текстовом редакторе, но лучше не редактировать его вручную, иначе есть риск случайного повреждения файла.

Теперь вы создали корневой или верхний узел сцены. Этот узел определяет общую функциональность объекта. Мы выбрали **Area2D**, потому что это 2D-узел, он может перемещаться в 2D-пространстве и обнаруживать перекрытия с другими узлами, так что мы сможем обнаруживать монеты и другие игровые объекты. Выбор узла для конкретного игрового объекта - это ваше первое важное решение при проектировании игровых объектов.

Прежде чем добавлять дочерние узлы, хорошей идеей будет убедиться, что вы не сможете их случайно перемещать или изменять размер, кликнув по ним. Выберите узел **Player** и наведите мышь на значок рядом с замком, **Group Selected Node(s)**:
![](/img/1-coin/8.png)

Подсказка говорит "Make selected node’s children not selectable." (Сделать дочерние узлы выбранных узлов невыбираемыми), и это хорошо - это поможет избежать ошибок. Нажмите на кнопку, и вы увидите тот же значок появившимся рядом с именем узла **Player**:

![](/img/1-coin/9.png)

Это хорошая идея делать это всегда при создании новой сцены. Если дочерние узлы объекта смещаются или изменяют масштаб, это может вызвать неожиданные ошибки и затруднить их устранение.


## Анимация спрайта

С использованием **Area2D** вы можете обнаруживать перекрытия или столкновения с другими объектами, но **Area2D** сам по себе не имеет своего внешнего вида. Вам также понадобится узел, который может отображать изображение. Поскольку у персонажа есть анимации, выберите узел player и добавьте узел **AnimatedSprite2D**. Этот узел будет отвечать за внешний вид и анимации игрока. Обратите внимание, что рядом с узлом есть символ предупреждения. **AnimatedSprite2D** требует наличие ресурса **SpriteFrames**, который содержит анимацию(и), которую он может отображать. Чтобы создать этот ресурс, найдите свойство Frames в окне Inspector и щелкните по <empty>, чтобы увидеть выпадающий список. Выберите New **SpriteFrames**:

![](/img/1-coin/10.png)


Затем в том же месте нажмите на метку SpriteFrames, которая появилась там, чтобы открыть новую панель внизу экрана:

![](/img/1-coin/11.png)

Слева находится список анимаций. Нажмите на анимацию по умолчанию и переименуйте ее в `run`. Затем нажмите кнопку **Add Animation** и создайте вторую анимацию с именем `idle` и третью с именем `hurt`.

В панели FileSystem слева найдите изображения для анимаций `run`, `idle` и `hurt` в папке `res://assets/player/` и перетащите их в соответствующие анимации:

![](/img/1-coin/12.png)


Каждая новая анимация имеет стандартную настройку скорости 5 кадров в секунду. Это немного медленно, поэтому выберите каждую из анимаций и установите Speed на 8.

Чтобы увидеть анимации в действии, нажмите кнопку Play (▶️). Ваши анимации появятся в окне **Inspector** в выпадающем списке для свойства **Animation**. Выберите одну из них, чтобы увидеть ее в действии:

![](/img/1-coin/13.png)

Вы также можете выбрать анимацию для воспроизведения по умолчанию. Выберите анимацию `idle` и нажмите кнопку **Autoplay on Load**.
![](/img/1-coin/14.png)

Позднее вы напишете код для выбора между этими анимациями в зависимости от того, что делает игрок. Тем не менее, сначала вам нужно завершить настройку узлов игрока.

Изображение игрока немного маленькое, поэтому установите свойство **Scale** **AnimatedSprite2D** на `(2, 2)`, чтобы увеличить его в масштабе. Вы можете найти это свойство в разделе **Transform** в окне **Inspector**.

![](/img/1-coin/15.png)

## Форма столкновения (Collision shape)

При использовании **Area2D** или одного из других объектов столкновения вам необходимо сообщить Godot, какова форма объекта. Его форма столкновения определяет область, которую он занимает, и используется для обнаружения перекрытий и/или столкновений. Формы определяются различными типами **Shape2D** и включают в себя прямоугольники, круги и многоугольники. В разработке игр это иногда называют хитбоксом.

Для удобства, когда вам нужно добавить форму к области или физическому телу, вы можете добавить **CollisionShape2D** как дочерний узел. Затем вы можете выбрать тип формы и редактировать ее размер в редакторе.

Добавьте **CollisionShape2D** в качестве дочернего узла к узлу **Player** (убедитесь, что вы не добавляете его как дочерний узел к **AnimatedSprite2D**). В окне **Inspector** найдите свойство Shape и нажмите `<empty>`, чтобы выбрать New **RectangleShape2D**.

![](/img/1-coin/16.png)

Перетащите оранжевые ручки, чтобы изменить размер формы так, чтобы она покрывала спрайт. Подсказка: если вы удерживаете клавишу Alt во время перетаскивания ручки, форма будет масштабироваться симметрично. Возможно, вы заметили, что форма столкновения не центрирована на спрайте. Это потому, что сами изображения спрайтов не центрированы вертикально. Вы можете исправить это, добавив небольшое смещение к ***AnimatedSprite2D***. Выберите узел и найдите свойство **Offset** в окне **Inspector**. Установите его в (0, -5).

![](/img/1-coin/17.png)

Когда вы закончите, ваша сцена **Player** должна выглядеть примерно так:

![](/img/1-coin/18.png)

## Создание скрипта для игрока(Scripting the player )
Теперь вы готовы добавить код для игрока. Прикрепление скрипта к узлу позволяет вам добавить дополнительную функциональность, которая не предоставляется самим узлом. Выберите узел **Player** и нажмите новую кнопку скрипта:

![](/img/1-coin/19.png)

В окне **Attach Node Script**, вы можете оставить настройки по умолчанию. Если вы запомнили сохранить сцену, скрипт будет автоматически назван в соответствии с именем сцены. Нажмите **Create**, и вы перейдете в окно скрипта. Ваш скрипт будет содержать некоторые комментарии и подсказки по умолчанию.

Первая строка каждого скрипта описывает, к какому типу узла он прикреплен. Сразу после этого вы можете начать определять свои переменные:

::godot
    extends Area2D
    @export var speed = 350
    var velocity = Vector2.ZERO
    var screensize = Vector2(480, 720) 
::


Использование аннотации **@export** для переменной **speed** позволяет устанавливать ее значение в окне **Inspector**, так же как и любое другое свойство узла. Это может быть очень удобным для значений, которые вы хотите легко настраивать. Выберите узел Player, и вы увидите, что свойство **Speed** теперь отображается в окне **Inspector**. Любое значение, установленное в окне Inspector, переопределит значение `350` скорости, которое вы написали в скрипте.

![](/img/1-coin/20.png)

Что касается других переменных, **velocity** будет содержать скорость и направление движения персонажа, а **screensize** поможет установить пределы движения персонажа. Позднее вы установите это значение автоматически из основной сцены игры, но пока установка его вручную позволит вам проверить, что все работает.

Далее вы будете использовать функцию `_process()` для определения действий игрока. Функция `_process()` вызывается на каждом кадре, поэтому вы можете использовать ее для обновления элементов вашей игры, которые ожидают частых изменений. На каждом кадре вам нужно, чтобы игрок выполнял три действия:

::list
  -  Проверять ввод с клавиатуры
  -  Двигаться в заданном направлении
  -  Воспроизводить соответствующую анимацию
::


Сначала вам нужно проверить ввод. Для этой игры у вас есть четыре направленных ввода (четыре стрелки клавиатуры). Действия ввода определяются в **Project Settings** в разделе  **Input Map**. В этой вкладке вы можете определять пользовательские события и присваивать им клавиши, действия мыши или другие входные устройства. По умолчанию в Godot события назначены стрелкам клавиатуры, поэтому вы можете использовать их для этого проекта.

Вы можете определить, удерживается ли действие ввода, используя `Input.is_action_pressed()`, который возвращает `true`, если клавиша удерживается, и `false`, если не удерживается. Комбинирование состояний всех четырех клавиш даст вам результирующее направление движения.

Вы можете сделать это, проверив все четыре клавиши отдельно с использованием нескольких операторов `if`, но так как это такая распространенная потребность, Godot предоставляет полезную функцию под названием `Input.get_vector()`, которая обработает это за вас – вам просто нужно сказать ей, какие четыре ввода использовать. Обратите внимание на порядок, в котором перечислены действия ввода; `get_vector()` ожидает их в этом порядке. Результат этой функции - вектор направления (direction vector), указывающий в одном из восьми возможных направлений, в результате нажатия клавиш: 
::python
    func _process(delta):
        velocity = Input.get_vector("ui_left", "ui_right",
        "ui_up", "ui_down")
        position += velocity * speed * delta 
::

После этого у вас будет вектор скорости, указывающий, в каком направлении двигаться, поэтому следующим шагом будет фактическое обновление позиции игрока с использованием этой скорости.

Нажмите кнопку Run Current Scene (F6) в правом верхнем углу и проверьте, можете ли вы перемещать игрока, используя все четыре стрелки клавиатуры.

Вы можете заметить, что игрок продолжает выходить за край экрана. Вы можете использовать функцию `clamp()`, чтобы ограничить позицию игрока минимальными и максимальными значениями, предотвращая их уход за пределы экрана. Добавьте эти две строки далее, сразу после предыдущей строки:
::python
    position.x = clamp(position.x, 0, screensize.x)
    position.y = clamp(position.y, 0, screensize.y) 
::

## delta

Функция `_process()` включает параметр, называемый delta, который затем умножается на `velocity`. Что такое delta?

Игровой движок пытается работать с постоянной частотой 60 кадров в секунду. Однако это может измениться из-за замедления компьютера, либо в Godot, либо из-за других программ, работающих на вашем компьютере одновременно. Если частота кадров не постоянна, это повлияет на движение объектов в вашей игре. Например, представьте объект, который вы хотите перемещать на 10 пикселей на каждом кадре. Если все идет гладко, это означает, что объект перемещается на 600 пикселей за секунду. Однако, если некоторые из этих кадров занимают чуть больше времени, то в течение этой секунды может быть всего 50 кадров, и объект переместится всего на 500 пикселей.

Godot, как и многие игровые движки и фреймворки, решает эту проблему, передавая вам значение под названием delta, которое представляет собой прошедшее время с предыдущего кадра. Большую часть времени это будет очень близко к 0,016 секунд (приблизительно 16 миллисекунд). Если вы умножите вашу желаемую скорость 600 пикселей в секунду на delta, вы получите перемещение в точности на 10 пикселей. Однако, если delta увеличится до 0,3 секунды, то объект переместится на 18 пикселей. В целом скорость движения остается постоянной и независимой от частоты кадров.

Как побочное преимущество, вы можете выражать свое движение в единицах пикселей в секунду, а не пикселей на кадр, что легче визуализировать.

Теперь, когда игрок может двигаться, вам нужно изменить анимацию, которую проигрывает **AnimatedSprite2D**, в зависимости от того, двигается ли игрок или стоит на месте. Изображение для анимации бега направлено вправо, что означает, что его нужно перевернуть горизонтально (с использованием свойства *Flip H*, которое вы можете видеть в окне *Inspector* - попробуйте переключить его), когда движется влево. Добавьте этот код в вашу функцию `_process()` после кода движения:
::godot
    if velocity.length() > 0:
        $AnimatedSprite2D.animation = "run"
    else:
        $AnimatedSprite2D.animation = "idle"
    if velocity.x != 0:
        $AnimatedSprite2D.flip_h = velocity.x < 0 
::  

При использовании обозначения `$` имя узла относится к узлу, выполняющему скрипт. Например, `$Node1/Node2` будет ссылаться на узел (Node2), который является дочерним для `Node1`, который сам является дочерним для узла, выполняющего скрипт. Автозаполнение Godot будет предлагать имена узлов при вводе. Обратите внимание, что если имя содержит пробелы, вы должны заключить его в кавычки - например, `$"Мой узел"`.

Заметьте, что этот код делает небольшое уклонение. *flip_h* - это булево свойство, что означает, что оно может быть *true* или *false*. Булево значение также является результатом сравнения, такого как <. Из-за этого вы можете непосредственно установить свойство равным результату сравнения.

Воспроизведите сцену снова и проверьте, что анимации корректны в каждом случае.

## Начало и окончание движения игрока

Главная сцена должна уведомить игрока, когда игра началась и закончилась. Для этого добавьте функцию `start()` к игроку, которая установит начальное положение и анимацию игрока:
::gdscript
    func start():
        set_process(true)
        position = screensize / 2
        $AnimatedSprite2D.animation = "idle" 
::

Также добавьте функцию `die()`, которая будет вызываться, когда игрок сталкивается с препятствием или истекает время:
::gdscript
    func die():
        $AnimatedSprite2D.animation = "hurt"
        set_process(false) 
::

Использование `set_process(false)` говорит Godot'у прекратить вызывать функцию `_process()` каждый кадр. Поскольку код движения находится в этой функции, после завершения игры вы больше не сможете двигаться.

## Подготовка к столкновениям

Игрок должен обнаруживать, когда он касается монеты или препятствия, но вы еще не создали эти объекты. Но это не проблема, потому что вы можете использовать функциональность сигналов в Godot, чтобы заставить это работать. Сигналы - это способ для узлов отправлять сообщения, которые другие узлы могут обнаруживать и на них реагировать. У многих узлов есть встроенные сигналы, чтобы предупреждать вас о событиях, таких как столкновение с телом или нажатие кнопки. Вы также можете определить собственные сигналы для своих целей.
Сигналы используются путем их подключения к узлу (или узлам), которые вы хотите слушать. Это подключение можно сделать в окне Inspector или в коде. Позднее в проекте вы узнаете, как подключать сигналы обоими способами.
Добавьте следующие строки в верхнюю часть скрипта (после extends Area2D):
::gdscript
    signal pickup
    signal hurt 
::

Эти строки объявляют собственные сигналы, которые ваш игрок будет испускать, когда он касается монеты или препятствия. Касания будут обнаруживаться самим объектом *Area2D*. Выберите узел *Player* и нажмите вкладку **Node** рядом с вкладкой **Inspector**, чтобы увидеть список сигналов, которые игрок может испускать:

![Список сигналов узла.](/img/1-coin/21.png)

Заметьте также ваши собственные сигналы там. Поскольку другие объекты также будут узлами **Area2D**, вы захотите использовать сигнал **area_entered**. Выберите его и нажмите *Connect*. В появившемся окне снова нажмите Connect - вам не нужно изменять никакие из этих настроек. Godot автоматически создаст новую функцию с именем `_on_area_entered()` в вашем скрипте.

При подключении сигнала, вместо того чтобы заставить Godot создать функцию для вас, вы также можете указать имя существующей функции, которую вы хотите использовать вместо этого. Включите переключатель Make Function, если вы не хотите, чтобы Godot создавал функцию для вас.

Добавьте следующий код в эту новую функцию:

::godot
    func _on_area_entered(area):
        if area.is_in_group("coins"):
            area.pickup()
            pickup.emit()
        if area.is_in_group("obstacles"):
            hurt.emit()
            die() 
::


Когда другой объект области перекрывается с игроком, эта функция будет вызвана, и перекрывающаяся область будет передана с параметром area. У объекта монеты будет функция pickup(), которая определяет, что делать с монетой при подборе (воспроизведение анимации или звука, например). Когда вы создадите монеты и препятствия, вы присвоите им соответствующую группу, чтобы их можно было правильно обнаруживать.

Для подытожим, вот полный скрипт игрока на данный момент:
::

    extends Area2D

    signal pickup
    signal hurt

    @export var speed = 350

    var velocity = Vector2.ZERO
    var screensize = Vector2(480, 720)


    func _process(delta):
        # Get input for 8-directional movement
        velocity = Input.get_vector("ui_left", "ui_right", "ui_up", "ui_down")
        
        # Move the player's position
        position += velocity * speed * delta
        position.x = clamp(position.x, 0, screensize.x)
        position.y = clamp(position.y, 0, screensize.y)
        
        # Choose which animation to play
        if velocity.length() > 0:
            $AnimatedSprite2D.animation = "run"
        else:
            $AnimatedSprite2D.animation = "idle"
            
        # Flip the sprite based on movement direction
        if velocity.x != 0:
            $AnimatedSprite2D.flip_h = velocity.x < 0


        func start():
            set_process(true)
            position = screensize / 2
            $AnimatedSprite2D.animation = "idle"


        func die():
            $AnimatedSprite2D.animation = "hurt"
            set_process(false)

            
        func _on_area_entered(area):
            if area.is_in_group("coins"):
                area.pickup()
                pickup.emit("coin")
            if area.is_in_group("powerups"):
                area.pickup()
                pickup.emit("powerup")
            if area.is_in_group("obstacles"):
                hurt.emit()
                die()


::

Вы завершили настройку объекта игрока, и вы проверили, что движение и анимации работают правильно. Прежде чем перейти к следующему этапу, пройдитесь по настройке сцены игрока и коду и убедитесь, что вы понимаете, что вы сделали и почему. В следующем разделе вы создадите объекты, которые игрок сможет собирать.
