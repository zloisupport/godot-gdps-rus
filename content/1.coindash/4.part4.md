# Часть 4  Пользовательский интерфейс

Последний элемент, который нужен вашей игре, - это пользовательский интерфейс (UI). Он будет отображать информацию, которую игрок должен видеть во время игры и часто называется "головным дисплеем" (heads-up display HUD), поскольку информация появляется поверх игрового изображения. Вы также будете использовать этот сценарий для отображения кнопки начала после завершения игры.

Ваш HUD будет отображать следующую информацию:
::list
- Счет
- Оставшееся время
- Сообщение, например, "Конец игры"
- Кнопка начала игры
::

## Настройка узла

Создайте новую сцену и добавьте узел CanvasLayer с именем HUD. Узел CanvasLayer создает новый слой рисования, что позволяет вам рисовать элементы пользовательского интерфейса (UI) поверх остальной части игры, чтобы его не перекрывали объекты игры, такие как игрок или монеты. Godot предоставляет разнообразные элементы пользовательского интерфейса, которые могут использоваться для создания всего от индикаторов, таких как полосы здоровья, до сложных интерфейсов, таких как инвентари. Фактически, редактор Godot, который вы используете для создания этой игры, построен с использованием элементов управления Godot. Базовые узлы для UI расширяются из Control и появляются с зелеными значками в списке узлов. Для создания вашего пользовательского интерфейса вы будете использовать различные узлы Control для позиционирования, форматирования и отображения информации. Вот как будет выглядеть HUD при завершении:

![HUD](/img/1-coin/25.png)

## Cообщения


Добавьте в сцену узел Label и измените его имя на Message. Этот ярлык будет отображать заголовок игры, а также сообщение Game Over, когда игра заканчивается. Этот ярлык должен быть центрирован на игровом экране. Вы можете перемещать его с помощью мыши или устанавливать значения непосредственно в окне Inspector, но легче использовать ярлыки, предоставленные в меню макета, которые устанавливают значения за вас. Выберите HCenter Wide из меню макета:

![HUD](/img/1-coin/26.png)

Теперь ярлык занимает всю ширину экрана и центрируется по вертикали. Свойство **Text** устанавливает текст, который отображается ярлыком. Установите его на Coin Dash!, а горизонтальное и вертикальное выравнивание оба установите в Center.

Шрифт по умолчанию для узлов **Label** довольно мал и непривлекательный, поэтому следующим шагом будет назначение пользовательского шрифта. В свойстве **Label Settings** выберите **New LabelSettings**, а затем щелкните по нему, чтобы развернуть. Из вкладки FileSystem перетащите файл шрифта `Kenney Bold.ttf` и бросьте его в свойство **Font**, затем установите `Size` на `48`. Вы также можете улучшить внешний вид, добавив тень - попробуйте настройки, показанные на следующем скриншоте, или экспериментируйте с вашими собственными:

![HUD](/img/1-coin/27.png)

## Отображение счета и времени


Верхняя часть HUD будет отображать счет игрока и оставшееся время на часах. Оба будут представлены узлами Label, расположенными по противоположным сторонам игрового экрана. Вместо того чтобы располагать их отдельно, вы используете узел-контейнер для управления их позициями.

### Контейнеры

Контейнеры в Godot автоматически устанавливают позиции и размеры своих дочерних узлов Control (включая другие контейнеры). Вы можете использовать их для добавления отступов вокруг элементов, их центрирования или для их расположения в рядах и столбцах. У каждого типа контейнера есть специальные свойства, которые управляют способом их распределения по дочерним элементам.

Помните, что контейнеры автоматически распределяют своих детей. Если вы попытаетесь переместить или изменить размер Control, который находится внутри узла-контейнера, вы получите предупреждение от редактора. Вы можете вручную располагать элементы управления или располагать их с использованием контейнера, но не и то, и другое.


## Отображение счета и времени

Для управления метками счета и времени добавьте узел **MarginContainer** в *HUD*. Используйте меню компоновки, чтобы установить якоря на **Top Wide**. В разделе **Theme Overrides/Constants** окна **Inspector** установите четыре свойства *Margin* на 10. Это добавит некоторые поля, чтобы текст не прижимался к краю экрана.

Поскольку метки счета и времени будут использовать те же настройки шрифта, что и Message, вы можете сэкономить время, продублировав их. Выберите Message и нажмите Ctrl + D дважды, чтобы создать две дублированные метки. Перетащите обе и бросьте их на **MarginContainer**, чтобы сделать их его дочерними элементами. Назовите одного ребенка Score, а другого Time, и установите свойство Text в 0 для обоих. Установите **Vertical Alignment** в **Center** для обоих и Horizontal Alignment в Right для Score, но Left для Time.

## Обновление пользовательского интерфейса с использованием GDScript

Добавьте сценарий к узлу HUD. Этот сценарий будет обновлять элементы пользовательского интерфейса, когда их свойства будут изменяться, например, обновление текста **Score** при сборе монеты. Вот код:

```gdscript

    extends CanvasLayer
    signal start_game
    func update_score(value):
        $MarginContainer/Score.text = str(value)
    func update_timer(value):
        $MarginContainer/Time.text = str(value) 

```

Сценарий основной сцены будет вызывать эти две функции для обновления отображения при изменении значения. Для метки Message вам также нужен таймер, чтобы сделать его невидимым после короткого периода времени.

Добавьте узел таймера в качестве дочернего элемента HUD, установите время ожидания на 2 секунды и включите One Shot. Это гарантирует, что таймер будет запущен только один раз при старте, а не повторяться. Добавьте следующий код:

```gdscript

    func show_message(text):
        $Message.text = text
        $Message.show()
        $Timer.start() 

```


В этой функции вы отобразите сообщение и запустите таймер. Чтобы скрыть сообщение, подключитесь к сигналу timeout таймера (помните, что он автоматически создаст новую функцию):

```gdscript
    func _on_timer_timeout():
        $Message.hide()

```

## Создание Кнопки

Добавьте узел Button к HUD и измените его имя на StartButton. Эта кнопка будет отображаться перед началом игры, и, когда на нее нажмут, она скроется и отправит сигнал в сцену Main для запуска игры. Установите свойство Text в Start, затем прокрутите вниз к Theme Overrides/Fonts и установите шрифт, как вы делали с Message.

В меню компоновки выберите Center Bottom, чтобы центрировать кнопку внизу экрана. 

Когда кнопка нажимается, она генерирует сигнал. Во вкладке Node для StartButton подключитесь к сигналу pressed:

```gdscript
    func _on_start_button_pressed():
        $StartButton.hide()
        $Message.hide()
        start_game.emit() 
```

## Конец игры 


Последней задачей для вашего сценария пользовательского интерфейса (UI) является реакция на завершение игры:

```gdscript
    func show_game_over():
        show_message("Game Over")
        await $Timer.timeout
        $StartButton.show()
        $Message.text = "Coin Dash!"
        $Message.show() 
```

В этой функции вам нужно, чтобы сообщение "Game Over" отображалось в течение двух секунд, а затем исчезло, что и делает `show_message("Game Over")`. Однако после этого вы хотите показать кнопку запуска и заголовок игры, когда сообщение исчезнет. Команда `await` приостанавливает выполнение функции до тех пор, пока данный узел (Timer) не отправит сигнал (timeout). После получения сигнала функция продолжается, и все возвращается к начальному состоянию, чтобы вы могли сыграть снова.


Следующим шагом является настройка взаимодействия между Main и HUD. Добавьте экземпляр HUD в Main. В Main подключите сигнал timeout от GameTimer и добавьте следующий код, чтобы каждый раз, когда GameTimer срабатывает (каждую секунду), оставшееся время уменьшалось:

```gdscript
    func _on_game_timer_timeout():
        time_left -= 1
        $HUD.update_timer(time_left)
        if time_left <= 0:
            game_over() 
```

Далее выберите экземпляр Player в Main и подключите его сигналы pickup и hurt:
```gdscript
    func _on_player_hurt():
        game_over()
    func _on_player_pickup():
        score += 1
        $HUD.update_score(score) 
```

Когда игра заканчивается, нужно выполнить несколько действий, поэтому добавьте следующую функцию:

```gdscript
    func game_over():
        playing = false
        $GameTimer.stop()
        get_tree().call_group("coins", "queue_free")$HUD.show_game_over()
        $Player.die() 
```
    

Эта функция останавливает игру и также использует `call_group()` для удаления всех оставшихся монет, вызывая `queue_free()` для каждой из них.

Наконец, нажатие кнопки StartButton должно активировать функцию `new_game()` в Main. Выберите экземпляр HUD и подключите его сигнал `start_game`:
```gdscript
    func _on_hud_start_game():
        new_game() 
```
Убедитесь, что вы удалили `new_game()` из функции `_ready()` в Main (помните, что это было только для тестирования), и добавьте эти две строки в `new_game()`:

```gdscript
    $HUD.update_score(score)
    $HUD.update_timer(time_left)
```

Теперь вы можете играть в игру! Проверьте, что все части работают так, как задумано: счет, отсчет времени, завершение и перезапуск игры и так далее. Если вы обнаружите, что какая-то часть не работает, вернитесь и проверьте шаг, на котором вы ее создали, а также шаг(и), на котором она может быть подключена к остальной части игры. Очень часто ошибкой является забывчивость подключить один из многих сигналов, используемых в различных частях игры.

Как только вы сыграете в игру и убедитесь, что все работает правильно, вы можете перейти к следующему разделу, где вы сможете добавить несколько дополнительных функций, чтобы сделать игровой опыт более насыщенным.